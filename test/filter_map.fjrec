type bool
data constructor True : {} -> bool
data constructor False : {} -> bool

type int
data constructor Zero : {} -> int
data constructor Succ : { int } -> int

type list a
data constructor Nil : forall a. {} -> list a
data constructor Cons : forall a. { a ; list a } -> list a

program
  let rec map [a] [b] (f : a -> b) (l : list a) : list b =
    match l return list b with
    | Nil [_] {} -> Nil [b] {}
    | Cons [_] { x ; l } ->
        let x = f x in
        let l = map [a] [b] f l in
        Cons [b] { x ; l }
    end
  in

  let rec filter [a] (f : a -> bool) (l : list a) : list a =
    match l return list a with
    | Nil [_] {} -> Nil [a] {}
    | Cons [_] { x ; l } ->
        match f x return list a with
        | False {} -> filter [a] f l
        | True {} ->
            let l = filter [a] f l in
            Cons [a] { x ; l }
        end
    end
  in

  let filter_map [a] [b] (f : a -> bool) (g : a -> b) (l : list a) : list b =
    let l = filter [a] f l in
    let l = map [a] [b] g l in
    l
  in

  let is_zero (n : int) : bool =
    match n return bool with
    | Zero {} -> True {}
    | Succ { _ } -> False {}
    end
  in
  let plus2 (n : int) : int = Succ { Succ { n } } in

  let zero = Zero {} in
  let one = Succ { zero } in
  let l = Cons [int] { zero ; Cons [int] { one ; Nil [int] {} } } in

  let l = filter_map [int] [int] is_zero plus2 l in
  l
